plugins {
    id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion" apply false
    id 'org.jetbrains.kotlin.kapt' version "$kotlinVersion" apply false
}

allprojects {
    group = 'codes.fdk.blueprint.api'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    plugins.withType(JavaPlugin).configureEach {

        java {
            modularity.inferModulePath = true
            sourceCompatibility = JavaVersion.VERSION_15
        }

        ext {
            mapstructVersion = '1.4.1.Final'
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
                mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR8'
                mavenBom 'org.testcontainers:testcontainers-bom:1.15.0'
            }
            dependencies {
                dependency "org.mapstruct:mapstruct:${mapstructVersion}"
                dependency "org.mapstruct:mapstruct-processor:${mapstructVersion}"
                dependency 'com.github.javafaker:javafaker:1.0.2'
            }
        }

        configurations.all {
            exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
        }

        compileJava {
            doFirst {
                options.compilerArgs += [
                        '--module-path', classpath.asPath,
                ]
            }
            options.compilerArgs += [
                    '--enable-preview',
                    '-Xlint:-module'
            ]
        }

        compileTestJava {
            options.compilerArgs += ['--enable-preview']
        }

        test {
            jvmArgs '--enable-preview'
            useJUnitPlatform()
        }

    }

    plugins.withId("org.jetbrains.kotlin.jvm") {

        dependencyManagement {
            imports {
                mavenBom "org.jetbrains.kotlin:kotlin-bom:${kotlinVersion}"
                mavenBom 'io.vertx:vertx-dependencies:4.0.0'
            }
        }

        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_14
                freeCompilerArgs += [
                        '-XXLanguage:-NewInference',
                        '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
                ]
            }
        }

        test {
            jvmArgs += '-Dvertx.disableDnsResolver=true'
        }

    }

}