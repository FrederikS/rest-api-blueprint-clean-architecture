plugins {
    id 'java'
    id 'java-library'
    id 'org.springframework.boot' version '2.3.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java-test-fixtures'
}

allprojects {
    group = 'codes.fdk.blueprint.api'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '15'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    ext {
        mapstructVersion = '1.4.1.Final'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR8"
        }
        dependencies {
            dependency "org.mapstruct:mapstruct:${mapstructVersion}"
            dependency "org.mapstruct:mapstruct-processor:${mapstructVersion}"
            dependency 'com.github.javafaker:javafaker:1.0.2'
        }
    }

    java {
        modularity.inferModulePath = true
    }

    compileJava {
        options.compilerArgs += ["--enable-preview"]
    }

    compileTestJava {
        options.compilerArgs += ["--enable-preview"]
    }

    test {
        jvmArgs '--enable-preview'
        useJUnitPlatform()
    }
}

repositories {
    mavenCentral()
}

jar.enabled = false
bootJar.enabled = false
